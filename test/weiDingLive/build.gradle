buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.nemerosa:versioning:+'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
    }
}

defaultTasks 'clean', 'build', 'uploadArchives'

ext {
    versions = [:]
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'net.nemerosa.versioning'

    group = 'zs.live'
    version = System.getenv('LIVE_VERSION') ?: versioning.info.full ?: new Date().format('YYYY-MM-dd.HH-mm-ss').replaceAll('-', '')

    println "==> building $group.$name-$version"

    uploadArchives {
        repositories.mavenDeployer {
            repository(url: "file://${System.getenv('REPO_DIR') ?: rootProject.buildDir}/repo/")
        }
    }

    configurations {
//        all*.exclude group: "log4j", module: "log4j"
        all {
            resolutionStrategy {
                eachDependency { d ->
                    if (!d.target.version)
                        d.useVersion versions["${d.target.group}:${d.target.name}"] ?: versions[d.target.group] ?: 'latest.release'
                }
            }
        }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8
buildDir = new File(rootProject.buildDir, project.name)

repositories {
    
    maven { url 'http://repo.spring.io/release/' }
    jcenter()
    mavenCentral()
}


description = '直播微服务'

dependencies {
    compile fileTree(dir:'lib', include:'*.jar')

    compile 'org.slf4j:slf4j-api:1.7.6'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.2'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.1.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.8'
    compile 'org.codehaus.gpars:gpars:1+'
    compile 'org.jsoup:jsoup:1.9.1'
    compile 'com.google.code.gson:gson:2.7'

    compile 'com.alibaba:fastjson:1.2.28'

    compile 'org.springframework.boot:spring-boot-starter-web:1+'
    compile 'org.springframework:spring-web:4+'
    compile 'org.springframework.boot:spring-boot:1+'
    compile 'org.springframework.cloud:spring-cloud-starter-consul-all:1.0.2.RELEASE', {
        exclude group: 'org.bouncycastle'
    }

    // https://mvnrepository.com/artifact/org.apache.ivy/ivy
    compile group: 'org.apache.ivy', name: 'ivy', version: '2.3.0'
    compile 'org.apache.tomcat:tomcat-jdbc'

    runtime 'mysql:mysql-connector-java'
    compile 'org.mongodb:mongo-java-driver'
    compile 'redis.clients:jedis'
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'com.googlecode.xmemcached:xmemcached:2.0.0'
    compile 'org.apache.kafka:kafka_2.11:0.8+'
    compile 'com.baidubce:bce-java-sdk:0.10.25'
    runtime 'org.springframework.boot:spring-boot-starter-actuator'
}

configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}
